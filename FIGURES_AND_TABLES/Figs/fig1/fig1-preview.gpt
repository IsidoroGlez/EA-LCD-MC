reset
set auto
set fit noerrorscaling

##############
#COLOR PALETE
##############
PALETTE=0
load "my_color_palette.gpt"

###############
# APS TERMINAL
###############
C=1.  # One column
R=.75  # Square ratio
load "APS_term.gpt"

#############
# OUTPUT
############
set output "fig1-preview.tex"

############
# DATA
###########

dir="DATA/"

set style fill solid 1.0 noborder

set xrange [-0.5:7.5]
set yrange [-0.5:512.5]

set ylabel '\scriptsize $z$' offset 4.5
set ytics 0,64,514 offset 0.8
set xlabel '\scriptsize $x$' offset 0,1.5
set xtics 1,2,8 offset 0,0.65
set format x '\scriptsize $%.0f$'
set format y '\scriptsize $%.0f$'

set tics scale 0
unset key
unset colorbox

#set multiplot layout 1,9 spacing 0.005,0 margin 0.075,0.925,0.055,0.965
set multiplot

set bmargin screen 0.1
set tmargin screen 0.935	

x_margin=0.11
x_offset=x_margin
panel_w=(1-x_offset-x_margin)/8.

do for [i=0:7]{
   set origin x_offset + i*panel_w, 0
   set size panel_w, 1.0
   
   set rmargin 0
   set lmargin 0

   set title "\\scriptsize $y=".i."$" offset 0,-0.9

   if (i==7){
      set ytics mirror
      set format y ""
      set y2range [-0.5:512.5]
      set y2tics 0,64,514 offset -0.75
      set format y2 "\\scriptsize $%.0f$"
      set y2label "\\scriptsize $z$" offset -5
   }
   plot "< awk '{if($2==".i.") print $1,$3}' ".dir."cluster_0000_001_n" u 1:2:(0.5):(0.5) w boxxyerror lc rgb colorGet("c2") not,\
   "< awk '{if($2==".i.") print $1,$3}' ".dir."cluster_0001_001_n" u 1:2:(0.5):(0.5) w boxxyerror lc rgb colorGet("c5") not,\
   "< awk '{if($2==".i.") print $1,$3}' ".dir."cluster_0002_001_p" u 1:2:(0.5):(0.5) w boxxyerror lc rgb colorGet("c4") not,\
   "< awk '{if($2==".i.") print $1,$3}' ".dir."cluster_0003_001_n" u 1:2:(0.5):(0.5) w boxxyerror lc rgb colorGet("c3") not,\
   "< awk '{if($2==".i.") print $1,$3}' ".dir."cluster_0004_001_p" u 1:2:(0.5):(0.5) w boxxyerror lc rgb colorGet("c7") not,\
   "< awk '{if($2==".i.") print $1,$3}' ".dir."cluster_0005_001_p" u 1:2:(0.5):(0.5) w boxxyerror lc rgb colorGet("c6") not,\
   "< awk '{if($2==".i.") print $1,$3}' ".dir."cluster_0006_001_p" u 1:2:(0.5):(0.5) w boxxyerror lc rgb colorGet("c1") not,\
   "< awk '{if($2==".i.") print $1,$3}' ".dir."cluster_0007_001_p" u 1:2:(0.5):(0.5) w boxxyerror lc rgb colorGet("c8") not
   
   unset ytics
   unset ylabel
}

unset multiplot


###########
# CLOSE
###########

set out

#############
# CREATE PDF
#############
!pdflatex fig1-preview.tex
!pdf2svg fig1-preview.pdf fig1-preview.svg
#!latex fig1-preview.tex
#!dvisvgm --no-fonts fig1-preview.dvi

!rm *.aux *.tex *conver* *.log *.eps *.dvi