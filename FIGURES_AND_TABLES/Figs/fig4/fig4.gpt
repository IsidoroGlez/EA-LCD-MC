reset
set auto
set fit noerrorscaling

##############
#COLOR PALETE
##############
PALETTE=0
load "my_color_palette.gpt"

###############
# APS TERMINAL
###############
C=1.  # One column
R=.75  # Square ratio
load "APS_term.gpt"

#############
# OUTPUT
############
set output "fig4.tex"

############
# DATA
###########

set multiplot

set tmargin screen 0.98
set bmargin screen 0.14

set lmargin screen 0.15
set rmargin screen 0.98

OBC_4R="< paste DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n0_NBLOCKS_16384.txt DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n1_NBLOCKS_16384.txt | awk '($1==0){print $4,$5,$11,$12}'"
OBC_6R="< paste DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n0_NBLOCKS_16384.txt DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n2_NBLOCKS_16384.txt | awk '($1==0){print $4,$5,$11,$12}'"
OBC_8R="< paste DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n0_NBLOCKS_16384.txt DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n3_NBLOCKS_16384.txt | awk '($1==0){print $4,$5,$11,$12}'"

OBC_4R_Tc="< paste DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n0_NBLOCKS_16384.txt DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n1_NBLOCKS_16384.txt | awk '(($1==\"I\")&&($2==0.9075)){print $4,$5,$11,$12}'"
OBC_6R_Tc="< paste DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n0_NBLOCKS_16384.txt DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n2_NBLOCKS_16384.txt | awk '(($1==\"I\")&&($2==0.9075)){print $4,$5,$11,$12}'"
OBC_8R_Tc="< paste DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n0_NBLOCKS_16384.txt DATA/OBC/INDEPENDENT/16x48/C_2nR_over_C_2R_Q_Q_n3_NBLOCKS_16384.txt | awk '(($1==\"I\")&&($2==0.9075)){print $4,$5,$11,$12}'"

set xlabel '\footnotesize $C^{(1)}(r)$' offset 0,0.9
set ylabel '\footnotesize $C^{(n)}(r)/[C^{(1)}(r)]^n$' offset 3.7

set yrange [1:200]
set xrange [0.02:0.5]

set xtics 0.1,0.1,0.4 offset 0,0.5
set ytics offset 0.5

set format '\footnotesize %g'

set logs

x0=2.1e-2

set label 1 '\scriptsize $n=2$' left at x0,4.1
set label 2 '\scriptsize $n=3$' left at x0,23
set label 3 '\scriptsize $n=4$' left at x0,175

x0=0.3
y0=8
set label 4 '\scriptsize $T=0.7$' left at x0+0.005,y0+0.5

set arrow 1 nohead from x0,y0 to 0.21,7
set arrow 2 nohead from x0,y0 to 0.21,3.2
set arrow 3 nohead from x0,y0 to 0.21,1.7

x0=0.16
y0=15
set label 8 '\scriptsize $T=1.1019$' left at x0+0.005,y0+3
set label 9 '\scriptsize $[T_\mathrm{c}=1.1019(29)]$' left at x0+0.005,y0-0.8

set arrow 4 nohead from x0,y0 to 0.1,7
set arrow 5 nohead from x0,y0 to 0.1,3 
set arrow 6 nohead from x0,y0 to 0.1,1.6

set object 100 rect from 0.055,17 to 0.059,20 front lw 1 fc rgb "gray" fillstyle empty

plot OBC_4R u 1:3:2:4 w xyerror pt 0 lc rgb colorGet("c6") not,\
OBC_6R u 1:3:2:4 w xyerror pt 0 lc rgb colorGet("c2") not,\
OBC_8R u 1:3:2:4 w xyerror pt 0 lc rgb colorGet("c7") not,\
OBC_4R_Tc u 1:3:2:4 w xyerror pt 0 lc rgb colorGet("c6") not,\
OBC_6R_Tc u 1:3:2:4 w xyerror pt 0 lc rgb colorGet("c2")  not,\
OBC_8R_Tc u 1:3:2:4 w xyerror pt 0 lc rgb colorGet("c7")  not

unset object
unset ylabel
unset xlabel
unset label
unset arrow

set tmargin screen 1
set bmargin screen 0.6
set lmargin screen 0.4
set rmargin screen 0.8

unset logs

set xrange [0.055:0.059]
set yrange [17:20]

# # Primer bloque (negro) â€” de abajo hacia arriba

x=0.0564
dx=0.0001
y=18.1
dy=0.1
do for[i=0:9]{
   if ( (i%2)==0 ){
      set object (i+1) rect from x-dx,y to x,y+dy fc rgb "black"
      set object (i+11) rect from x,y to x+dx,y+dy fc rgb "white"
   } else {
      set object (i+1) rect from x-dx,y to x,y+dy fc rgb "white"
      set object (i+11) rect from x,y to x+dx,y+dy fc rgb "black"
   }
   y=y+dy
}

set label 100 '\scriptsize $1$' center at x-0.0003, y-10*dy+0.5 rotate

x=0.0565
dx=0.0001
y=17.9
dy=0.1
do for[i=0:9]{
   if ( (i%2)==0 ){
      set object (i+21) rect from x,y to x+dx,y+dy fc rgb "black"
      set object (i+31) rect from x,y+dy to x+dx,y+2*dy fc rgb "white"
   } else {
      set object (i+21) rect from x,y to x+dx,y+dy fc rgb "white"
      set object (i+31) rect from x,y+dy to x+dx,y+2*dy fc rgb "black"
   }
   x=x+dx
}
set label 200 '\scriptsize $1 \times 10^{-3}$' center at x-2*dx, y-0.17

set object 200 rect from 0.0555,17.5 to 0.0585,19.5 front lw 1 fc rgb "gray" fillstyle empty

unset border
unset tics

set arrow 1 nohead from 0.0555,19.5 to screen 0.4104,screen 0.6142
set arrow 2 nohead from 0.0585,17.5 to screen 0.4286, screen 0.5888

replot

unset multiplot

###########
# CLOSE
###########

set out

#############
# CREATE PDF
#############
!pdflatex fig4.tex
!latex fig4.tex
!dvisvgm --no-fonts fig4.dvi
!rm *.aux *.tex *conver* *.log *.eps *.dvi

