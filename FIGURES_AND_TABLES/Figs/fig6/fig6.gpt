reset
set auto
set fit noerrorscaling

##############
#COLOR PALETE
##############
PALETTE=0
load "my_color_palette.gpt"

###############
# APS TERMINAL
###############
C=1.  # One column
R=.75  # Square ratio
load "APS_term.gpt"

#############
# OUTPUT
############
set output "fig6.tex"

############
# DATA
###########

inv_xi_L4=system('paste DATA/PBC/4x192/xi01_Q_Q_n0_NBLOCKS_32768.txt DATA/PBC/4x192/inv_xi01_Q_Q_n0_NBLOCKS_32768.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\" && \$3>=\$4){print \$11; exit}"')
inv_xi_L4_err=system('paste DATA/PBC/4x192/xi01_Q_Q_n0_NBLOCKS_32768.txt DATA/PBC/4x192/inv_xi01_Q_Q_n0_NBLOCKS_32768.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\" && \$3>=\$4){print \$12; exit}"')
L4="< awk '$1==0{print $3,$6,$7}' DATA/PBC/4x192/C_Q3_over_C_Q_n0_NBLOCKS_32768.txt"

inv_xi_L6=system('paste DATA/PBC/6x192/xi01_Q_Q_n0_NBLOCKS_32768.txt DATA/PBC/6x192/inv_xi01_Q_Q_n0_NBLOCKS_32768.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\" && \$3>=\$4){print \$11; exit}"')
inv_xi_L6_err=system('paste DATA/PBC/6x192/xi01_Q_Q_n0_NBLOCKS_32768.txt DATA/PBC/6x192/inv_xi01_Q_Q_n0_NBLOCKS_32768.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\" && \$3>=\$4){print \$12; exit}"')
L6="< awk '$1==0{print $3,$6,$7}' DATA/PBC/6x192/C_Q3_over_C_Q_n0_NBLOCKS_32768.txt"

inv_xi_L8=system('paste DATA/PBC/8x192/xi01_Q_Q_n0_NBLOCKS_12288.txt DATA/PBC/8x192/inv_xi01_Q_Q_n0_NBLOCKS_12288.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\" && \$3>=\$4){print \$11; exit}"')
inv_xi_L8_err=system('paste DATA/PBC/8x192/xi01_Q_Q_n0_NBLOCKS_12288.txt DATA/PBC/8x192/inv_xi01_Q_Q_n0_NBLOCKS_12288.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\" && \$3>=\$4){print \$12; exit}"')
L8="< awk '$1==0{print $3,$6,$7}' DATA/PBC/8x192/C_Q3_over_C_Q_n0_NBLOCKS_12288.txt"

inv_xi_L12=system('paste DATA/PBC/12x320/xi01_Q_Q_n0_NBLOCKS_32768.txt DATA/PBC/12x320/inv_xi01_Q_Q_n0_NBLOCKS_32768.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\" && \$3>=\$4){print \$11; exit}"')
inv_xi_L12_err=system('paste DATA/PBC/12x320/xi01_Q_Q_n0_NBLOCKS_32768.txt DATA/PBC/12x320/inv_xi01_Q_Q_n0_NBLOCKS_32768.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\" && \$3>=\$4){print \$12; exit}"')
L12="< awk '$1==0{print $3,$6,$7}' DATA/PBC/12x320/C_Q3_over_C_Q_n0_NBLOCKS_32768.txt"

inv_xi_L16=system('paste DATA/OBC/INDEPENDENT/16x48/xi_Q_Q_n0_NBLOCKS_16384.txt DATA/OBC/INDEPENDENT/16x48/inv_xi_Q_Q_n0_NBLOCKS_16384.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\"){print \$4,\$11}" | sort -k1 -g | tail -1 | awk "{print \$2}"')
inv_xi_L16_err=system('paste DATA/OBC/INDEPENDENT/16x48/xi_Q_Q_n0_NBLOCKS_16384.txt DATA/OBC/INDEPENDENT/16x48/inv_xi_Q_Q_n0_NBLOCKS_16384.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\"){print \$4,\$12}" | sort -k1 -g | tail -1 | awk "{print \$2}"')
L16="< awk '$1==0{print $3,$6,$7}' DATA/OBC/INDEPENDENT/16x48/C_Q3_over_C_Q_n0_NBLOCKS_16384.txt"

inv_xi_L24=system('paste DATA/OBC/INDEPENDENT/24x88/xi_Q_Q_n0_NBLOCKS_2048.txt DATA/OBC/INDEPENDENT/24x88/inv_xi_Q_Q_n0_NBLOCKS_2048.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\"){print \$4,\$11}" | sort -k1 -g | tail -1 | awk "{print \$2}"')
inv_xi_L24_err=system('paste DATA/OBC/INDEPENDENT/24x88/xi_Q_Q_n0_NBLOCKS_2048.txt DATA/OBC/INDEPENDENT/24x88/inv_xi_Q_Q_n0_NBLOCKS_2048.txt 2>/dev/null \
 | awk "(\$1==0 && \$5!=\"-nan\"){print \$4,\$12}" | sort -k1 -g | tail -1 | awk "{print \$2}"')
L24="< awk '$1==0{print $3,$6,$7}' DATA/OBC/INDEPENDENT/24x88/C_Q3_over_C_Q_n0_NBLOCKS_2048.txt"

set tmargin screen 0.98
set bmargin screen 0.14

set lmargin screen 0.115
set rmargin screen 0.985

set xlabel '\footnotesize $r/\xi_{n=1}(L)$' offset 0, 1
set ylabel '\footnotesize $E(r)$' offset 4.5
set format '\footnotesize $%g$'
set xtics offset 0,0.5

set ytics 0.86,0.02,1 offset 0.75
set format y ''
set ytics add ('\footnotesize $0.9$' 0.9, '\footnotesize $1$' 1)

f(x)= (A + (1-A) * exp(-x*(U-2))) / (B+(1-B)*exp(-x*(U-2)))

A=1
U=16
B=1
fit [0.02:*] f(x) L24 u ($1*inv_xi_L24):2:($1*inv_xi_L24_err):3 xyerror via A,U,B

set xrange [-0.02:1]
set yrange [0.86:1.005]

set key at 0.7,0.985 reverse sample 0.75 keywidth 0.25 width -3 Left

plot \
L24 u ($1*inv_xi_L24):2:($1*inv_xi_L24_err):3 w xyerror pt 5 lc rgb colorGet("c6") t '\scriptsize $L=24$',\
L16 u ($1*inv_xi_L16):2:($1*inv_xi_L16_err):3 w xyerror pt 7 lc rgb colorGet("c5") t '\scriptsize $L=16$',\
L12 u ($1*inv_xi_L12):2:($1*inv_xi_L12_err):3 w xyerror pt 13 lc rgb colorGet("c2") t '\scriptsize $L=12$',\
L8 u ($1*inv_xi_L8):2:($1*inv_xi_L8_err):3 w xyerror pt 9 lc rgb colorGet("c4") t '\scriptsize $L=8$',\
L6 u ($1*inv_xi_L6):2:($1*inv_xi_L6_err):3 w xyerror pt 11 lc rgb colorGet("c7") t '\scriptsize $L=6$',\
L4 u ($1*inv_xi_L4):2:($1*inv_xi_L4_err):3 w xyerror pt 1 lc rgb colorGet("c8") t '\scriptsize $L=4$',\
f(x) lt -1 dt 2 lc rgb "black" not


unset multiplot

###########
# CLOSE
###########

set out

#############
# CREATE PDF
#############
!pdflatex fig6.tex
!latex fig6.tex
!dvisvgm --no-fonts fig6.dvi
!rm *.aux *.tex *conver* *.log *.eps *.dvi

