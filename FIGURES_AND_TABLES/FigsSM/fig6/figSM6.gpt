reset
set auto
set fit noerrorscaling

##############
#COLOR PALETE
##############
PALETTE=0
load "my_color_palette.gpt"

###############
# APS TERMINAL
###############
C=1.  # One column
R=.75  # Square ratio
load "APS_term.gpt"

#############
# OUTPUT
############
set output "figSM6.tex"

############
# DATA
###########

L24_MIN_0="< awk '($1==0) && $($4==\"nan\"){print $3,$4,$5}' DATA/OBC/INDEPENDENT/24x88/xi_2R_over_xi_MIN_Q3signQ_n0_NBLOCKS_2048.txt"
L24_MIN_1="< awk '($1==0) && $($4==\"nan\"){print $3,$4,$5}' DATA/OBC/INDEPENDENT/24x88/xi_2R_over_xi_MIN_signQQ_n0_NBLOCKS_2048.txt"
L24_MIN_2="< awk '($1==0) && $($4==\"nan\"){print $3,$4,$5}' DATA/OBC/INDEPENDENT/24x88/xi_2R_over_xi_MIN_QQ3_n0_NBLOCKS_2048.txt"

L16_MIN_0="< awk '($1==0) && $($4==\"nan\"){print $3,$4,$5}' DATA/OBC/INDEPENDENT/16x48/xi_2R_over_xi_MIN_Q3signQ_n0_NBLOCKS_16384.txt"
L16_MIN_1="< awk '($1==0) && $($4==\"nan\"){print $3,$4,$5}' DATA/OBC/INDEPENDENT/16x48/xi_2R_over_xi_MIN_signQQ_n0_NBLOCKS_16384.txt"
L16_MIN_2="< awk '($1==0) && $($4==\"nan\"){print $3,$4,$5}' DATA/OBC/INDEPENDENT/16x48/xi_2R_over_xi_MIN_QQ3_n0_NBLOCKS_16384.txt"

set lmargin screen 0.11
set rmargin screen 0.985
set tmargin screen 0.975
set bmargin screen 0.12

set xlabel '\footnotesize $r_{\mathrm{min}}$' offset 0,1.2
set ylabel '\footnotesize $U$' offset 3.8

set format '\footnotesize $%g$'

set xtics offset 0,0.5
set ytics offset 0.75
set xrange [-0.15:7]
set yrange [8:29]

U=22.2
U_err=0.7

set object 1 rect from -0.15,U-U_err to 7,U+U_err fc rgb "#aaccee" fs sol back 
set arrow 100 nohead from -0.15, U to 7,U lc rgb "black"

set label 1 '\scriptsize $\lbrace Q^3, \text{sign}(Q)\rbrace$' left at 0,13.2
set label 2 '\scriptsize $\lbrace Q, \text{sign}(Q)\rbrace$' left at 0,11.7
set label 3 '\scriptsize $\lbrace Q, Q^3\rbrace$' left at 0,10.2
set label 4 '\scriptsize $L\!=\!24$' center at 2.3,14.5
set label 5 '\scriptsize $L\!=\!16$' center at 3.3,14.5

plot \
L24_MIN_0 u 1:(1+$2):3 w yerror pt 7 lc rgb colorGet("c2") not,\
"< echo '2.3 12.06 0.3'" u 1:(1+$2):3 w xerror pt 7 ps 1.5 lw 2 lc rgb colorGet("c2") not,\
L24_MIN_1 u ($1+0.15):(1+$2):3 w yerror pt 5 lc rgb colorGet("c7") not,\
"< echo '2.3 10.56 0.3'" u 1:(1+$2):3 w xerror pt 5 ps 1.5 lw 2 lc rgb colorGet("c7") not,\
L24_MIN_2 u ($1+0.30):(1+$2):3 w yerror pt 13 lc rgb colorGet("c5") not,\
"< echo '2.3 9.06 0.3'" u 1:(1+$2):3 w xerror pt 13 ps 2 lw 2 lc rgb colorGet("c5") not,\
L16_MIN_0 u ($1+0.45):(1+$2):3 w yerror pt 6 lc rgb colorGet("c3") not,\
"< echo '3.3 12.06 0.3'" u 1:(1+$2):3 w xerror pt 6 ps 1.5 lw 2 lc rgb colorGet("c3") not,\
L16_MIN_1 u ($1+0.60):(1+$2):3 w yerror pt 4 lc rgb colorGet("c6") not,\
"< echo '3.3 10.56 0.3'" u 1:(1+$2):3 w xerror pt 4 ps 1.5 lw 2 lc rgb colorGet("c6") not,\
L16_MIN_2 u ($1+0.75):(1+$2):3 w yerror pt 12 lc rgb colorGet("c4") not,\
"< echo '3.3 9.06 0.3'" u 1:(1+$2):3 w xerror pt 12 ps 2 lw 2 lc rgb colorGet("c4") not


###########
# CLOSE
###########

set out

#############
# CREATE PDF
#############
!pdflatex figSM6.tex
!latex figSM6.tex
!dvisvgm --no-fonts figSM6.dvi
!rm *.aux *.tex *conver* *.log *.eps *.dvi
