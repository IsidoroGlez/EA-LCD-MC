reset
set auto
set fit noerrorscaling

##############
#COLOR PALETE
##############
PALETTE=0
load "my_color_palette.gpt"

###############
# APS TERMINAL
###############
C=1.  # One column
R=.75  # Square ratio
load "APS_term.gpt"

#############
# OUTPUT
############
set output "figSM6.tex"

############
# DATA
###########

Lz32="./DATA/Lz32_xi01-xi12.txt"
Lz48="./DATA/Lz48_xi01-xi12.txt"
Lz64="./DATA/Lz64_xi01-xi12.txt"
Lz96="./DATA/Lz96_xi01-xi12.txt"
Lz128="./DATA/Lz128_xi01-xi12.txt"
Lz192="./DATA/Lz192_xi01-xi12.txt"

set lmargin screen 0.11
set rmargin screen 0.985
set tmargin screen 0.975
set bmargin screen 0.12

set xlabel '\footnotesize $r_{\mathrm{min}}$' offset 0,1.2
set ylabel '\footnotesize $\xi_{n=1}$' offset 3.8

set format x '\footnotesize $%g$'

set xtics offset 0,0.5
unset ytics
set ytics 17.8,0.2 format "" offset 0.75
set ytics add ('\footnotesize $18$' 18, '\footnotesize $19$' 19)

set label 1 '\footnotesize $M\!=\!96$' left at 30, 17.8
set arrow 1 nohead from 29, 17.8 to 25,17.8

set label 2 '\footnotesize $M\!=\!128$' left at 36, 18.4
set arrow 2 nohead from 35.5, 18.4 to 33,18.52

set label 3 '\footnotesize $M\!=\!192$' center at 43, 19.1
set arrow 3 nohead from 35, 18.93 to 38,19.05


set xrange [-0.5:49]
set yrange [17.7:19.3]

plot \
Lz96 u 1:2:3 every 2 w yerror pt 6 ps 1.5 lw 2 lc rgb colorGet("c8") not "96",\
Lz96 u 1:4:5 every 2 w yerror pt 7 ps 1.5 lc rgb colorGet("c8") not "96",\
Lz128 u 1:2:3 every 2 w yerror pt 8 ps 1.5 lw 2 lc rgb colorGet("c6") not "128",\
Lz128 u 1:4:5 every 2 w yerror pt 9 ps 1.5 lc rgb colorGet("c6") not "128",\
Lz192 u 1:2:3 every 4 w yerror pt 10 ps 1.5 lw 2 lc rgb colorGet("c7") not "192",\
Lz192 u 1:4:5 every 4::2 w yerror pt 11 ps 1.5 lc rgb colorGet("c7") not "192"

###########
# CLOSE
###########

set out

#############
# CREATE PDF
#############
!pdflatex figSM6.tex
!latex figSM6.tex
!dvisvgm --no-fonts figSM6.dvi
!rm *.aux *.tex *conver* *.log *.eps *.dvi
