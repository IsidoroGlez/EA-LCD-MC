reset
set auto
set fit noerrorscaling

##############
#COLOR PALETE
##############
PALETTE=0
load "my_color_palette.gpt"

###############
# APS TERMINAL
###############
C=1.  # One column
R=.75  # Square ratio
load "APS_term.gpt"

#############
# OUTPUT
############
set output "figSM7.tex"

############
# DATA
###########

Lz32="./DATA/Lz32_xi01-xi12.txt"
Lz48="./DATA/Lz48_xi01-xi12.txt"
Lz64="./DATA/Lz64_xi01-xi12.txt"
Lz96="./DATA/Lz96_xi01-xi12.txt"
Lz128="./DATA/Lz128_xi01-xi12.txt"
Lz192="./DATA/Lz192_xi01-xi12.txt"

set lmargin screen 0.11
set rmargin screen 0.985
set tmargin screen 0.975
set bmargin screen 0.12

set xlabel '\footnotesize $r_{\mathrm{min}}$' offset 0,1.2
set ylabel '\footnotesize $\xi_{n=1}$' offset 3.8

set format x '\footnotesize $%g$'

set xtics offset 0,0.5
unset ytics
set ytics 17.8,0.2 format "" offset 0.75
set ytics add ('\footnotesize $18$' 18, '\footnotesize $19$' 19)

set label 1 '\scriptsize $M\!=\!96$' left at 25,18.3
set label 2 '\scriptsize $M\!=\!128$' left at 25,18.15
set label 3 '\scriptsize $M\!=\!192$' left at 25,18.

set label 4 '\scriptsize $\xi_{01}$' center at 37,18.45
set label 5 '\scriptsize $\xi_{12}$' center at 44,18.45

set xrange [-0.5:49]
set yrange [17.7:19.3]

plot \
Lz96 u 1:2:3 every 2 w yerror pt 6 ps 1.5 lw 2 lc rgb colorGet("c8") not "96",\
"< echo '37 18.3 2.5'" u 1:2:3 w xerror pt 6 ps 1.5 lw 2 lc rgb colorGet("c8") not,\
Lz96 u 1:4:5 every 2 w yerror pt 7 ps 1.5 lw 2 lc rgb colorGet("c8") not "96",\
"< echo '44 18.3 2.5'" u 1:2:3 w xerror pt 7 ps 1.5 lw 2 lc rgb colorGet("c8") not,\
Lz128 u 1:2:3 every 2 w yerror pt 8 ps 1.5 lw 2 lc rgb colorGet("c6") not "128",\
"< echo '37 18.15 2.5'" u 1:2:3 w xerror pt 8 ps 2 lw 2 lc rgb colorGet("c6") not,\
Lz128 u 1:4:5 every 2 w yerror pt 9 ps 1.5 lw 2 lc rgb colorGet("c6") not "128",\
"< echo '44 18.15 2.5'" u 1:2:3 w xerror pt 9 ps 2.5 lw 2 lc rgb colorGet("c6") not,\
Lz192 u 1:2:3 every 4 w yerror pt 10 ps 1.5 lw 2 lc rgb colorGet("c7") not "192",\
"< echo '37 18.0 2.5'" u 1:2:3 w xerror pt 10 ps 2 lw 2 lc rgb colorGet("c7") not,\
Lz192 u 1:4:5 every 4::2 w yerror pt 11 ps 1.5 lw 2 lc rgb colorGet("c7") not "192",\
"< echo '44 18.0 2.5'" u 1:2:3 w xerror pt 11 ps 2.5 lw 2 lc rgb colorGet("c7") not


###########
# CLOSE
###########

set out

#############
# CREATE PDF
#############
!pdflatex figSM7.tex
!latex figSM7.tex
!dvisvgm --no-fonts figSM7.dvi
!rm *.aux *.tex *conver* *.log *.eps *.dvi
