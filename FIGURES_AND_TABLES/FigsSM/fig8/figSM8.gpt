reset
set auto
set fit noerrorscaling

##############
#COLOR PALETE
##############
PALETTE=0
load "my_color_palette.gpt"

###############
# APS TERMINAL
###############
C=1.  # One column
R=0.75  # Square ratio
load "APS_term.gpt"

#############
# OUTPUT
############
set output "figSM8.tex"

############
# DATA
###########

L16="DATA/L16_shortM.tot"

set lmargin screen 0.16
set rmargin screen 0.995
set tmargin screen 0.978
set bmargin screen 0.11

set xlabel '\footnotesize $M$' offset 0,1.2
set ylabel '\footnotesize $R(M)$' offset 4.5

set format '\footnotesize $%g$'

set xtics offset 0,0.5
set ytics offset 0.75

set title "L=16"
set xrange [1:16.5]
set yrange [.74:1.0]
f(x)=a*exp(-x/xi)+b*exp(-x/xi2)
g(x)=a*exp(-x/xi)
xi=44
xi2=2
fit [:16] f(x) L16 u 2:9:10 yerr via a,b,xi,xi2

plot L16 u 2:9:10 w yerr pt 9 ps 2 lc rgb colorGet("c2") not,\
f(x) dt 2 lc "black" not,\
g(x) lc "black" not


###########
# CLOSE
###########

set out

#############
# CREATE PDF
#############
!pdflatex figSM8.tex
!latex figSM8.tex
!dvisvgm --no-fonts figSM8.dvi
!rm *.aux *.tex *conver* *.log *.eps *.dvi
